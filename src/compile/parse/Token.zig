const Token = @This();

const std = @import("std");
const Io = std.Io;
const assert = std.debug.assert;
const testing = std.testing;

const Traps = @import("../../Traps.zig");
const Span = @import("../Span.zig");
const integers = @import("integers.zig");

span: Span,
value: Value,

pub const Error =
    integers.Error ||
    error{
        InvalidDirective,
        UnknownDirective,
        InvalidLabel,
        InvalidToken,
        UnmatchedQuote,
    };

pub fn from(span: Span, source: []const u8, traps: *const Traps) Error!Token {
    const value: Value = try .from(span.view(source), traps);
    return .{ .span = span, .value = value };
}

pub const Value = union(enum) {
    newline,
    comma,
    colon,

    directive: Directive,
    instruction: Instruction,
    trap_alias: u8,
    label,

    register: u3,
    integer: integers.SourceInt(16),
    /// Contained in `Token.span`.
    string: Span,

    pub const Directive = enum {
        orig,
        end,
        fill,
        blkw,
        stringz,
    };

    pub const Instruction = enum {
        // Arithmetic
        add,
        @"and",
        not,
        // Branch
        br,
        brn,
        brz,
        brp,
        brnz,
        brzp,
        brnp,
        brnzp,
        // Jump
        jmp,
        ret,
        jsr,
        jsrr,
        // Load / store
        lea,
        ld,
        st,
        ldi,
        sti,
        ldr,
        str,
        // Trap *aliases* are handled separatly
        trap,
        // Extension instructions
        push,
        pop,
        call,
        rets,
        // Only used in 'supervisor' mode
        rti,
    };

    pub fn from(string: []const u8, traps: *const Traps) Error!Value {
        assert(string.len > 0);

        // Trap aliases always take precedence
        if (try tryTrap(string, traps)) |value|
            return value;

        const parsers = [_]fn ([]const u8) Error!?Value{
            // Order is important
            tryKeyword,
            tryRegister,
            tryInteger,
            tryString,
            tryDirective,
            tryInstruction,
            tryLabel,
        };
        inline for (parsers) |parser| {
            if (try parser(string)) |value|
                return value;
        }
        return error.InvalidToken;
    }

    fn tryKeyword(string: []const u8) Error!?Value {
        return if (std.mem.eql(u8, string, "\n"))
            .newline
        else if (std.mem.eql(u8, string, ","))
            .comma
        else if (std.mem.eql(u8, string, ":"))
            .colon
        else
            null;
    }

    fn tryRegister(string: []const u8) Error!?Value {
        if (string.len != 2)
            return null;
        switch (string[0]) {
            'r', 'R' => {},
            else => return null,
        }
        const register: u3 = switch (string[1]) {
            '0'...'7' => |char| @intCast(char - '0'),
            else => return null,
        };
        return .{ .register = register };
    }

    fn tryInteger(string: []const u8) Error!?Value {
        const integer = try integers.tryInteger(string) orelse
            return null;
        return .{ .integer = integer };
    }

    fn tryString(string: []const u8) Error!?Value {
        if (string.len < 2)
            return null;
        const has_initial_quote = string[0] == '"';
        const has_final_quote = string[string.len - 1] == '"';
        if (!has_initial_quote and !has_final_quote)
            return null;
        if (!has_initial_quote or !has_final_quote)
            return error.UnmatchedQuote;
        return .{ .string = .fromBounds(1, string.len - 1) };
    }

    fn tryDirective(string: []const u8) Error!?Value {
        if (string.len < 2 or string[0] != '.')
            return null;
        const rest = string[1..];
        if (!isIdent(rest))
            return error.InvalidDirective;
        if (matchTagName(Directive, rest)) |directive| {
            return .{ .directive = directive };
        }
        return error.UnknownDirective;
    }

    fn tryInstruction(string: []const u8) Error!?Value {
        assert(string.len > 0);
        if (matchTagName(Instruction, string)) |instruction| {
            return .{ .instruction = instruction };
        }
        return null;
    }

    fn tryTrap(string: []const u8, traps: *const Traps) Error!?Value {
        assert(string.len > 0);
        for (traps.entries, 0..) |entry_opt, vect| {
            if (entry_opt) |entry| if (entry.alias) |alias|
                if (std.ascii.eqlIgnoreCase(string, alias))
                    return .{ .trap_alias = @intCast(vect) };
        }
        return null;
    }

    fn tryLabel(string: []const u8) Error!?Value {
        assert(string.len > 0);
        if (!isIdent(string[0..1]))
            return null;
        if (!isIdent(string))
            return error.InvalidLabel;
        return .label;
    }

    fn matchTagName(comptime T: type, string: []const u8) ?T {
        for (std.meta.tags(T)) |tag| {
            if (std.ascii.eqlIgnoreCase(string, @tagName(tag)))
                return tag;
        }
        return null;
    }

    fn isIdent(string: []const u8) bool {
        for (string, 0..) |char, i| {
            switch (char) {
                'a'...'z', 'A'...'Z', '_' => {},
                '0'...'9' => if (i == 0) return false,
                else => return false,
            }
        }
        return true;
    }
};
