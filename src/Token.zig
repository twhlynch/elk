const Token = @This();

const std = @import("std");
const Io = std.Io;
const assert = std.debug.assert;
const testing = std.testing;

const integers = @import("integers.zig");
const Integer = integers.Integer;
const Span = @import("Span.zig");

// TODO: Rename, since "kind" might imply a tag with no data
kind: Kind,
span: Span,

pub const Error = error{
    InvalidInteger,
    InvalidDirective,
    InvalidIdent,
    InvalidToken,
    UnmatchedQuote,
};

pub fn from(span: Span, source: []const u8) !Token {
    const kind: Kind = try .from(span.view(source));
    return .{ .kind = kind, .span = span };
}

pub const Kind = union(enum) {
    newline,
    comma,
    register: u3,
    integer: Integer(16),
    // TODO: Use Span ? right now there is not much benefit either way...
    string: []const u8,
    directive: Directive,
    instruction: Instruction,
    label,

    pub const Directive = enum {
        orig,
        end,
        fill,
        blkw,
        stringz,
    };

    pub const Instruction = enum {
        // Arithmetic
        add,
        @"and",
        not,
        // Branch
        br,
        brn,
        brz,
        brp,
        brnz,
        brzp,
        brnp,
        brnzp,
        // Jump
        jmp,
        ret,
        jsr,
        jsrr,
        // Load / store
        ld,
        st,
        ldi,
        sti,
        ldr,
        str,
        lea,
        // Traps
        trap,
        getc,
        out,
        puts,
        in,
        putsp,
        halt,
        // Extension traps
        reg,
        debug,
        // Only used in 'supervisor' mode
        rti,
    };

    pub fn from(string: []const u8) Error!Kind {
        assert(string.len > 0);
        const parsers = [_]fn ([]const u8) Error!?Kind{
            tryKeyword,
            tryRegister,
            tryInteger,
            tryString,
            tryDirective,
            tryInstruction,
            tryLabel,
        };
        inline for (parsers) |parser| {
            if (try parser(string)) |kind|
                return kind;
        }
        return error.InvalidToken;
    }

    fn tryKeyword(string: []const u8) Error!?Kind {
        return if (std.mem.eql(u8, string, "\n"))
            .newline
        else if (std.mem.eql(u8, string, ","))
            .comma
        else
            null;
    }

    fn tryRegister(string: []const u8) Error!?Kind {
        if (string.len != 2)
            return null;
        switch (string[0]) {
            'r', 'R' => {},
            else => return null,
        }
        const register: u3 = switch (string[1]) {
            '0'...'7' => |char| @intCast(char - '0'),
            else => return null,
        };
        return .{ .register = register };
    }

    fn tryInteger(string: []const u8) Error!?Kind {
        const integer = try integers.tryInteger(string) orelse
            return null;
        return .{ .integer = integer };
    }

    fn tryString(string: []const u8) Error!?Kind {
        if (string.len < 2)
            return null;
        const has_initial_quote = string[0] == '"';
        const has_final_quote = string[string.len - 1] == '"';
        if (!has_initial_quote and !has_final_quote)
            return null;
        if (!has_initial_quote or !has_final_quote)
            return error.UnmatchedQuote;
        return .{ .string = string[1 .. string.len - 1] };
    }

    fn tryDirective(string: []const u8) Error!?Kind {
        if (string.len < 2 or string[0] != '.')
            return null;
        const rest = string[1..];
        if (!isIdent(rest))
            return error.InvalidIdent;
        if (matchTagName(Directive, rest)) |directive| {
            return .{ .directive = directive };
        }
        return error.InvalidDirective;
    }

    fn tryInstruction(string: []const u8) Error!?Kind {
        assert(string.len > 0);
        if (matchTagName(Instruction, string)) |instruction| {
            return .{ .instruction = instruction };
        }
        return null;
    }

    fn tryLabel(string: []const u8) Error!?Kind {
        assert(string.len > 0);
        if (!isIdent(string[0..1]))
            return null;
        if (!isIdent(string))
            return error.InvalidIdent;
        return .label;
    }

    fn matchTagName(comptime T: type, string: []const u8) ?T {
        for (std.meta.tags(T)) |tag| {
            if (std.ascii.eqlIgnoreCase(string, @tagName(tag)))
                return tag;
        }
        return null;
    }

    fn isIdent(string: []const u8) bool {
        for (string, 0..) |char, i| {
            switch (char) {
                'a'...'z', 'A'...'Z', '_' => {},
                '0'...'9' => if (i == 0) return false,
                else => return false,
            }
        }
        return true;
    }
};
